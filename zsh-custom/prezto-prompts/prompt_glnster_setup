#
# A theme based on steeef theme
#  * Remote Git branch on the right
#  * Local Git branch info on the left
#  * Dual line prompt
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#   Glenn Cueto <glenncueto@gmail.com>

# Load dependencies.
pmodload 'helper'

function prompt_glnster_pwd {
  local pwd="${PWD/#$HOME/~}"
 
  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_glnster_pwd="$MATCH"
    unset MATCH
  else
    _prompt_glnster_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_glnster_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD
  prompt_glnster_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi
}

function prompt_glnster_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_glnster_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_glnster_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
      "%F{60}" # pale purple
      "%F{68}" # blueish
      "%F{249}" # 
      "%F{240}" # 
      "%F{232}" # 
    )
  else
    __PROMPT_glnster_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

# For display params
# https://github.com/sorin-ionescu/prezto/tree/master/modules/git
# %A - ahead count
# $B - behind count
# %S - stash count
# %C - clean state
# %D - dirty files count
# %U - unmerged count

  # git
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_glnster_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_glnster_COLORS[5]}+%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_glnster_COLORS[2]}-%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_glnster_COLORS[4]}✗%f"
  zstyle ':prezto:module:git:info:ahead' format "${__PROMPT_glnster_COLORS[7]}↑%f"
  zstyle ':prezto:module:git:info:behind' format "${__PROMPT_glnster_COLORS[7]}↓%f"
  zstyle ':prezto:module:git:info:stashed' format "${__PROMPT_glnster_COLORS[7]}✭%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_glnster_COLORS[3]}~%f"
  zstyle ':prezto:module:git:info:remote' format "${__PROMPT_glnster_COLORS[6]}⚡ %R%f"
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '(%b%m%d%a%u%A%B%S)' \
    'rprompt' '%R'

#user: ${__PROMPT_glnster_COLORS[3]}%n%f
#localhost: ${__PROMPT_glnster_COLORS[2]}%m%f
#pwd: %~
#time: %t
_lineup=$'\e[1A'
_linedown=$'\e[1B'
_moveright=$'\e[2C'
_moveleft=$'\e[2D'
_savepos=$'\e[s'
_restorepos=$'\e[u'

  PROMPT="$prompt_newline${__PROMPT_glnster_COLORS[8]}❯${__PROMPT_glnster_COLORS[9]}❯${__PROMPT_glnster_COLORS[10]}❯ "
  PROMPT+="${__PROMPT_glnster_COLORS[2]}%D{%L:%M:%S}%f ${__PROMPT_glnster_COLORS[5]}"'$_prompt_glnster_pwd'"%f "'$git_info[prompt]'"${prompt_newline}%# "
  RPROMPT="%{${_lineup}%}"'$git_info[rprompt]'"%{${_linedown}%}"
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_glnster_setup "$@"